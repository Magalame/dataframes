import Dataframes.Internal.CWrappers
import Dataframes.Internal.Utils
import Dataframes.Table
import Dataframes.IO.Options

import Std.Foreign
import Std.Foreign.C.Value

class ElementType:
    IntElement
    RealElement
    TextElement
    MaybeIntElement
    MaybeRealElement
    MaybeTextElement

    def toArrowId: case self of
        IntElement: 9
        RealElement: 12
        TextElement: 13
        MaybeIntElement: 9
        MaybeRealElement: 12
        MaybeTextElement: 13

    def nullable: case self of
        IntElement: False
        RealElement: False
        TextElement: False
        MaybeIntElement: True
        MaybeRealElement: True
        MaybeTextElement: True

def listToMap list:
    def insert (k,v) m: m.insert k v
    list.foldLeft Map.empty insert

class CSVParser:
    CSVParser
    CustomizedCSVParser ColumnNamePolicy [ElementType]

    def namePolicy: case self of
        CSVParser: TakeFromFirstRow
        CustomizedCSVParser n _: n

    def typePolicy: case self of
        CSVParser: []
        CustomizedCSVParser _ m: m

    def useCustomNames names:
        CustomizedCSVParser (CustomNames names) self.typePolicy

    def useReadColumnNames:
        CustomizedCSVParser TakeFromFirstRow self.typePolicy

    def useGenerateColumnNames:
        CustomizedCSVParser Generate self.typePolicy

    def setTypes columnTypes:
        CustomizedCSVParser self.namePolicy columnTypes

    def customNames: case self.namePolicy of
        CustomNames l: l
        _: []

    def readFile filepath:
        Table.fromWrapper $ callCsvParser (ParseCSVFile filepath) self.namePolicy self.typePolicy

    def readText text:
        Table.fromWrapper $ callCsvParser (ParseCSVContents text) self.namePolicy self.typePolicy

class XLSXParser:
    XLSXParser
    CustomizedXLSXParser ColumnNamePolicy [ElementType]

    def namePolicy: case self of
        XLSXParser: TakeFromFirstRow
        CustomizedXLSXParser n _: n

    def typePolicy: case self of
        XLSXParser: []
        CustomizedXLSXParser _ m: m

    def useCustomNames names:
        CustomizedXLSXParser (CustomNames names) self.typePolicy

    def useReadColumnNames:
        CustomizedXLSXParser TakeFromFirstRow self.typePolicy

    def useGenerateColumnNames:
        CustomizedXLSXParser Generate self.typePolicy

    def setTypes columnTypes:
        CustomizedXLSXParser self.namePolicy columnTypes

    def customNames: case self.namePolicy of
        CustomNames l: l
        _: []

    def readFile filepath:
        Table.fromWrapper $ callCsvParser (ParseXLSXFile filepath) self.namePolicy self.typePolicy

class CSVGenerator:
    CSVGenerator
    CSVGeneratorCustomized GeneratorHeaderPolicy CSVGeneratorQuotingPolicy

    def headerPolicy: case self of
        CSVGenerator: WriteHeaderLine
        CSVGeneratorCustomized hp _: hp
    def quotingPolicy: case self of
        CSVGenerator: QuoteWhenNeeded
        CSVGeneratorCustomized _ qp: qp
    def setHeaderPolicy hp:
        CSVGeneratorCustomized hp self.quotingPolicy
    def setQuotingPolicy qp:
        CSVGeneratorCustomized self.headerPolicy qp

    def generateText table:
        callHandlingError "writeTableToCsvString" CString [table.ptr.ptr.toCArg, self.headerPolicy.toCArg, self.quotingPolicy.toCArg] . toText
    def generateFile path table:
        CString.with path pathC:
            callHandlingError "writeTableToCsvFile" CString [pathC.toCArg, table.ptr.ptr.toCArg, self.headerPolicy.toCArg, self.quotingPolicy.toCArg] . toText


class XLSXGenerator:
    XLSXGenerator
    XLSXGeneratorCustomized GeneratorHeaderPolicy

    def headerPolicy: case self of
        XLSXGenerator: WriteHeaderLine
        XLSXGeneratorCustomized hp: hp
    def setHeaderPolicy hp:
        XLSXGeneratorCustomized hp
    def generateFile path table:
        CString.with path pathC:
            callHandlingError "writeTableToCsvFile" CString [pathC.toCArg, table.ptr.ptr.toCArg, self.headerPolicy.toCArg] . toText